def VERIFICATION_GROUP = 'Verification'

apply plugin: 'com.android.application'
apply plugin: 'checkstyle'

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.0"

    defaultConfig {
        applicationId "ca.raido.glSatelliteDemo"
        minSdkVersion 19
        targetSdkVersion 26

        ndk {
            abiFilters 'armeabi', 'armeabi-v7a', 'arm64-v8a','x86', 'x86_64'
        }
        externalNativeBuild {
            cmake {
                arguments '-DANDROID_PLATFORM=android-14',
                        '-DANDROID_TOOLCHAIN=clang', '-DANDROID_STL=c++_static'
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }

    externalNativeBuild {
        cmake {
            path 'src/main/cpp/CMakeLists.txt'
        }
    }

    lintOptions {
        disable 'TypographyDashes'
    }
}

repositories {
    maven { url "https://maven.google.com" }
}

dependencies {
    compile 'com.android.support:appcompat-v7:26.0.0-beta2'
    compile 'com.google.android.gms:play-services-ads:11.0.2'
}

task checkstyle(type: Checkstyle) {
    group VERIFICATION_GROUP
    description 'Checks the style of Java sources.'

    showViolations = true
    configFile file("checkstyle.xml")

    source 'src/main/java'
    include '**/*.java'
    exclude '**/gen/**'
    exclude '**/R.java'
    exclude '**/BuildConfig.java'

    // empty classpath
    classpath = files()
}

task infer(type: Exec) {
    group VERIFICATION_GROUP
    description 'Runs Infer check.'

    def inferCmd = 'infer'
    onlyIf {
        !"which $inferCmd".execute().waitFor()
    }
    def inferOpts = ['-o', "$buildDir/infer-out"]
    def gradleCmd = ['gradle', 'build', '--rerun-tasks']
    def cmd = [inferCmd] + inferOpts + ['--'] + gradleCmd
    commandLine(*cmd)
}

task tests(dependsOn: [check, infer, 'checkstyle']) {
    group VERIFICATION_GROUP
    description 'Runs all tests.'
}
